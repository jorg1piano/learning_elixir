# Starting a supervisor process
# iex(0)|> Supervisor.start_link([Todo.Cache], strategy: :one_for_one)
# iex(1)|> cache_pid = Process.whereis(Todo.Cache)
# iex(2)|> Process.exit(cache_pid, :kill)
#
defmodule Todo.Cache do
  use GenServer

  def start_link(_) do
    GenServer.start_link(__MODULE__, nil, name: __MODULE__)
  end

  def server_process(todo_list_name) do
    GenServer.call(__MODULE__, {:server_process, todo_list_name})
  end

  @impl true
  def init(_) do
    IO.puts("Starting to-do cache")
    Todo.Database.start()
    {:ok, %{}}
  end

  @impl true
  def handle_call({:server_process, todo_list_name}, _, todo_servers) do
    case Map.fetch(todo_servers, todo_list_name) do
      {:ok, todo_server} ->
        {:reply, todo_server, todo_servers}

      :error ->
        {:ok, new_server} = Todo.Server.start(todo_list_name)

        {
          :reply,
          new_server,
          Map.put(todo_servers, todo_list_name, new_server)
        }
    end
  end
end
